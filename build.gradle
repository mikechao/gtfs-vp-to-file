plugins {
	id 'java'
	id 'project-report'
	id 'jacoco'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.graalvm.buildtools.native' version '0.10.1'
}

group = 'dev.mike.chao'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

graalvmNative {
		testSupport = false
		binaries{
			all {
        resources.autodetect()
			}
			main {
				mainClass = 'dev.mike.chao.gtfsvptofile.GtfsVpToFileApplication'
				buildArgs.add("-march=native")
			}
		}
}

tasks.named("bootBuildImage") {
	environment["BP_NATIVE_IMAGE"] ="true"
	environment["BP_JVM_VERSION"] = "21"
	environment["BP_NATIVE_IMAGE_BUILD_ARGUMENTS"] = "--no-fallback -o gtfs-vp-to-file -R:MaxHeapSize=128m -R:MinHeapSize=32m"
	imageName = "docker.io/library/${project.name}:native.${project.version}"
}

bootJar  {
	manifest {
		attributes 'Start-Class': 'dev.mike.chao.gtfsvptofile.GtfsVpToFileApplication'
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven {
		url 'https://repo.camsys-apps.com/releases/'
	}
}

dependencies {
	implementation ('org.springframework.boot:spring-boot-starter-web') {
  	exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-core'
  	exclude group: 'org.apache.tomcat.embed', module: 'tomcat-embed-websocket'
	}
	String tomcatVersion = dependencyManagement.importedProperties['tomcat.version']
	implementation "org.apache.tomcat.experimental:tomcat-embed-programmatic:$tomcatVersion"
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'io.micrometer:micrometer-registry-prometheus'
	implementation 'org.mobilitydata:gtfs-realtime-bindings:0.0.8'
	implementation 'org.reflections:reflections:0.10.2'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.mockito:mockito-core:3.+"
	runtimeOnly("ch.qos.logback:logback-classic")
}

tasks.named('test') {
	useJUnitPlatform()
}